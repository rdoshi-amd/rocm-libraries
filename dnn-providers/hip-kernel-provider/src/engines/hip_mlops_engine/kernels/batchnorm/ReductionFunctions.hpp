// Copyright © Advanced Micro Devices, Inc., or its affiliates.
// SPDX-License-Identifier: MIT

#pragma once

#include "Configuration.hpp"
#include "StaticUnroll.hpp"

namespace hip_kernel_provider::batchnorm
{

namespace reduction
{

namespace detail
{

const unsigned long long FULL_MASK = 0xFFFFFFFFFFFFFFFFull;

template <int N>
struct Log2Floor
{
    constexpr static int VALUE = Log2Floor<(N >> 1)>::VALUE + 1;
};
template <>
struct Log2Floor<1>
{
    constexpr static int VALUE = 0;
};
template <int N>
constexpr static int LOG2_FLOOR_V = Log2Floor<N>::VALUE;

template <int N>
struct Log2Ceil
{
    constexpr static int VALUE = LOG2_FLOOR_V<N> + ((1 << LOG2_FLOOR_V<N>) == N ? 0 : 1);
};
template <int N>
constexpr static int LOG2_CEIL_V = Log2Ceil<N>::VALUE;

} // namespace detail

template <typename FloatAccum, unsigned int SizeLclData>
__forceinline__ __device__ void
    ldsReduce2(FloatAccum& x,
               FloatAccum& y,
               FloatAccum scale,
               FloatAccum (&lclDataX)[SizeLclData], // NOLINT(modernize-avoid-c-arrays)
               FloatAccum (&lclDataY)[SizeLclData], // NOLINT(modernize-avoid-c-arrays)
               unsigned int lid)
{
    lclDataX[lid] = x;
    lclDataY[lid] = y;
    __syncthreads();
    for(unsigned int red = (1 << detail::LOG2_CEIL_V<SizeLclData>) >> 1; red > 0; red >>= 1)
    {
        if(lid < red && lid + red < SizeLclData)
        {
            lclDataX[lid] += lclDataX[lid + red];
            lclDataY[lid] += lclDataY[lid + red];
        }
        __syncthreads();
    }

    x = lclDataX[0] * scale;
    y = lclDataY[0] * scale;
}

template <typename FloatAccumC, typename FloatAccum, unsigned int SizeLclData>
__forceinline__ __device__ void
    ldsReduce22d(FloatAccumC& x,
                 FloatAccumC& y,
                 FloatAccum scale,
                 FloatAccumC (&lclData)[SizeLclData], // NOLINT(modernize-avoid-c-arrays)
                 unsigned int xstride,
                 unsigned int xlid,
                 unsigned int ylid,
                 unsigned int /*size*/)
{
    const unsigned int offset1 = 2 * (xlid + ylid * xstride);
    // store the values by pairs (so the compiler will generate
    // one instruction to read/write them)
    lclData[offset1 + 0] = x;
    lclData[offset1 + 1] = y;

    __syncthreads();
    for(unsigned int red = (1 << detail::LOG2_CEIL_V<SizeLclData>) >> 1; red > 0; red >>= 1)
    {
        const unsigned int offset2 = offset1 + red * xstride * 2;
        if(ylid < red && offset2 < SizeLclData)
        {
            // make sure there is one read and one write
            x += lclData[offset2 + 0];
            y += lclData[offset2 + 1];
            lclData[offset1 + 0] = x;
            lclData[offset1 + 1] = y;
        }
        __syncthreads();
    }
    x = static_cast<FloatAccumC>(lclData[xlid * 2 + 0] * scale);
    y = static_cast<FloatAccumC>(lclData[xlid * 2 + 1] * scale);
}

template <typename FloatAccum>
__forceinline__ __device__ void dppInterleavedReduction(FloatAccum& tempSum1, FloatAccum& tempSum2)
{
    __asm__ volatile("s_nop 4\n"
                     "v_add_f32 %0 %0 %0 row_shr:1 bound_ctrl:0\n"
                     "v_add_f32 %1 %1 %1 row_shr:1 bound_ctrl:0\n"
                     "s_nop 0\n"
                     "v_add_f32 %0 %0 %0 row_shr:2 bound_ctrl:0\n"
                     "v_add_f32 %1 %1 %1 row_shr:2 bound_ctrl:0\n"
                     "s_nop 0\n"
                     "v_add_f32 %0 %0 %0 row_shr:4 bank_mask:0xe\n"
                     "v_add_f32 %1 %1 %1 row_shr:4 bank_mask:0xe\n"
                     "s_nop 0\n"
                     "v_add_f32 %0 %0 %0 row_shr:8 bank_mask:0xc\n"
                     "v_add_f32 %1 %1 %1 row_shr:8 bank_mask:0xc\n"
                     "s_nop 0\n"
                     "v_add_f32 %0 %0 %0 row_bcast:15 row_mask:0xa\n"
                     "v_add_f32 %1 %1 %1 row_bcast:15 row_mask:0xa\n"
                     "s_nop 0\n"
                     "v_add_f32 %0 %0 %0 row_bcast:31 row_mask:0xc\n"
                     "v_add_f32 %1 %1 %1 row_bcast:31 row_mask:0xc\n"
                     "s_nop 0"
                     : "=v"(tempSum1), "=v"(tempSum2)
                     : "0"(tempSum1), "1"(tempSum2));
}

template <typename FloatAccum, unsigned int SizeLclData>
__forceinline__ __device__ void
    gcnReduce2(FloatAccum& x,
               FloatAccum& y,
               FloatAccum scale,
               FloatAccum (&lclDataX)[SizeLclData], // NOLINT(modernize-avoid-c-arrays)
               FloatAccum (&lclDataY)[SizeLclData], // NOLINT(modernize-avoid-c-arrays)
               unsigned int lid)
{
    const unsigned int ldsidx = lid >> 6;
    dppInterleavedReduction(x, y);
    // Last thread
    if((lid % 64) == 63)
    {
        lclDataX[ldsidx] = x;
        lclDataY[ldsidx] = y;
    }

    __syncthreads();

    x = y = 0;

    // This could be changed to clang loop unroll(full), because the size is small
    StaticUnrollCount<unsigned int, 0, SizeLclData, 1, 2>{[&](unsigned int i) {
        x += lclDataX[i];
        y += lclDataY[i];
    }};

    x *= scale;
    y *= scale;
}

template <typename FloatAccum, unsigned int BlockSize>
__forceinline__ __device__ void
    reduce2(FloatAccum& x, FloatAccum& y, FloatAccum scale, unsigned int lid)
{
    static_assert(BlockSize > 0, "BlockSize must be positive");

    if constexpr(BlockSize == 1)
    {
        x *= scale;
        y *= scale;
        return;
    }

    if constexpr(BlockSize % 64 == 0)
    {
        for(unsigned int d = warpSize / 2; d >= 1; d >>= 1)
        {
            x += __shfl_down_sync(detail::FULL_MASK, x, d);
            y += __shfl_down_sync(detail::FULL_MASK, y, d);
        }

        if(BlockSize <= static_cast<unsigned int>(warpSize))
        {
            x = __shfl_sync(detail::FULL_MASK, x, 0) * scale;
            y = __shfl_sync(detail::FULL_MASK, y, 0) * scale;
            return;
        }

        constexpr unsigned int MAX_WARPS = BlockSize / 32;
        // NOLINTBEGIN(modernize-avoid-c-arrays, bugprone-dynamic-static-initializers)
        __shared__ FloatAccum s_x[MAX_WARPS];
        __shared__ FloatAccum s_y[MAX_WARPS];
        // NOLINTEND(modernize-avoid-c-arrays, bugprone-dynamic-static-initializers)

        const unsigned int lane = lid % static_cast<unsigned int>(warpSize);
        const unsigned int wid = lid / static_cast<unsigned int>(warpSize);
        const unsigned int numWarps = BlockSize / static_cast<unsigned int>(warpSize);

        if(lane == 0)
        {
            s_x[wid] = x;
            s_y[wid] = y;
        }
        __syncthreads();

        if(wid == 0)
        {
            x = FloatAccum{0};
            y = FloatAccum{0};
            for(unsigned int i = lane; i < numWarps; i += static_cast<unsigned int>(warpSize))
            {
                x += s_x[i];
                y += s_y[i];
            }
            for(unsigned int d = warpSize / 2; d >= 1; d >>= 1)
            {
                x += __shfl_down_sync(detail::FULL_MASK, x, d);
                y += __shfl_down_sync(detail::FULL_MASK, y, d);
            }
        }

        if(lid == 0)
        {
            s_x[0] = x * scale;
            s_y[0] = y * scale;
        }
        __syncthreads();
        x = s_x[0];
        y = s_y[0];
    }
    else
    {
        // Slow path, mainly for the unlikely case of a 32 thread block
        // NOLINTBEGIN(modernize-avoid-c-arrays, bugprone-dynamic-static-initializers)
        __shared__ FloatAccum s_x[BlockSize];
        __shared__ FloatAccum s_y[BlockSize];
        // NOLINTEND(modernize-avoid-c-arrays, bugprone-dynamic-static-initializers)

        s_x[lid] = x;
        s_y[lid] = y;
        __syncthreads();

        if(lid < static_cast<unsigned int>(warpSize))
        {
            x = FloatAccum{0};
            y = FloatAccum{0};
            for(unsigned int i = lid; i < BlockSize; i += static_cast<unsigned int>(warpSize))
            {
                x += s_x[i];
                y += s_y[i];
            }
            for(unsigned int d = warpSize / 2; d >= 1; d >>= 1)
            {
                x += __shfl_down_sync(detail::FULL_MASK, x, d);
                y += __shfl_down_sync(detail::FULL_MASK, y, d);
            }
        }

        if(lid == 0)
        {
            s_x[0] = x * scale;
            s_y[0] = y * scale;
        }
        __syncthreads();
        x = s_x[0];
        y = s_y[0];
    }
}

} // namespace reduction

} // namespace hip_kernel_provider::batchnorm
