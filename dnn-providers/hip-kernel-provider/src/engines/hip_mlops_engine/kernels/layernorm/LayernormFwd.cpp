// Copyright © Advanced Micro Devices, Inc., or its affiliates.
// SPDX-License-Identifier:  MIT

#include "LayernormCommon.hpp"

extern "C" __global__ void layernormFwd(const InputType* __restrict__ x,
                                        OutputType* __restrict__ y,
                                        const ScaleBiasType* __restrict__ scale,
                                        const ScaleBiasType* __restrict__ bias,
                                        MeanInvVarianceType* __restrict__ mean,
                                        MeanInvVarianceType* __restrict__ rstd,
                                        const float eps)
{
    const unsigned int gid = blockIdx.x; // NOLINT(readability-static-accessed-through-instance)
    const unsigned int lid = threadIdx.x; // NOLINT(readability-static-accessed-through-instance)
    const unsigned int o = gid / STRIDE;
    const unsigned int s = gid % STRIDE;

    __shared__ float s_ltmp1[LOCAL_SIZE];
    __shared__ float s_ltmp2[LOCAL_SIZE];
    __shared__ unsigned int s_ltmp3[LOCAL_SIZE];
    float pmean;
    float prstd;
    calculateMeanRstd(s_ltmp1, s_ltmp2, s_ltmp3, x, eps, lid, o, s, pmean, prstd);

    if(lid == 0)
    {
        if(mean != nullptr)
        {
            mean[gid] = hip_kernel_provider::cast<MeanInvVarianceType>(pmean);
        }
        if(rstd != nullptr)
        {
            rstd[gid] = hip_kernel_provider::cast<MeanInvVarianceType>(prstd);
        }
    }

    for(unsigned int i = lid; i < INNER_SIZE; i += LOCAL_SIZE)
    {
        const size_t idx = o * INNER_SIZE * STRIDE + i * STRIDE + s;

        const float pscale = (scale != nullptr) ? hip_kernel_provider::cast<float>(scale[i]) : 1.0f;
        const float pbias = (bias != nullptr) ? hip_kernel_provider::cast<float>(bias[i]) : 0.0f;

        const float val
            = (hip_kernel_provider::cast<float>(x[idx]) - pmean) * prstd * pscale + pbias;
        y[idx] = hip_kernel_provider::cast<OutputType>(val);
    }
}
